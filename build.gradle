import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'java'
    id "com.vanniktech.maven.publish" version "0.29.0"
    id 'signing'
}

group = 'io.github.miensoap'
version = '0.2.3'

repositories {
    mavenCentral()
}

dependencies {
    // feign
    implementation 'io.github.openfeign:feign-core:13.3'
    implementation 'io.github.openfeign:feign-gson:13.3'

    // yml
    implementation 'org.yaml:snakeyaml:2.0'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // test
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

signing {
    useGpgCmd()
    sign publishing.publications

}

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Empty(), true))
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    coordinates('io.github.miensoap', 'KISHelper', "0.2.3")
    pom {
        name = "KISHelper"
        description = "KISHelper: Library for Korea Investment Open API"
        inceptionYear = "2024"
        url = "https://github.com/Miensoap/KISHelper.git"
        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
            }
        }
        developers {
            developer {
                id = "Miensoap"
                name = "Miensoap"
                email = "miensop51@gmail.com"
                url = "https://github.com/Miensoap"
            }
        }
        scm {
            url = "https://github.com/Miensoap/KISHelper.git"
            connection = "scm:git@github.com:Miensoap/KISHelper"
            developerConnection = "scm:git@github.com:Miensoap/KISHelper.git"
        }
    }
}

sourceSets {
    main {
        java {
            exclude "**/Main.java"
        }
        resources {
            exclude "**"
        }
    }
}